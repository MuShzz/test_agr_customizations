



CREATE VIEW [cus].[v_ITEM] AS

WITH RankedProd AS (

	   SELECT
        CAST(P.[ProdNo] AS NVARCHAR(255)) AS [NO],
        CAST(ISNULL(P.[Descr], P.[ProdNo]) AS NVARCHAR(255)) AS [NAME],
        CAST(NULL AS NVARCHAR(1000)) AS [DESCRIPTION],
        CAST(P.[Gr5] AS NVARCHAR(255)) AS [PRIMARY_VENDOR_NO],
        CAST(ISNULL(D.[DelTm] + D.[TanspTm], 0) AS SMALLINT) AS [PURCHASE_LEAD_TIME_DAYS],
        CAST(NULL AS SMALLINT) AS [TRANSFER_LEAD_TIME_DAYS],
        CAST(NULL AS SMALLINT ) AS [ORDER_FREQUENCY_DAYS],
        CAST(NULL AS SMALLINT ) AS [ORDER_COVERAGE_DAYS],
        CAST(NULL AS DECIMAL(18,4)) AS [MIN_ORDER_QTY],
        CAST(D.SupProd AS NVARCHAR(50)) AS [ORIGINAL_NO],
        CAST(IIF(P.Gr3 IN (1,4,5), 0, 1) AS BIT) AS [CLOSED_FOR_ORDERING],
        CAST(A.Nm AS NVARCHAR(255)) AS [RESPONSIBLE],
        CAST(ISNULL(PDM.SalePr-PDM.SaleDcP*PDM.SalePr, 0) AS DECIMAL(18,4)) AS [SALE_PRICE],
        CAST(ISNULL(D.SupPrice, 0) AS DECIMAL(18,4)) AS [COST_PRICE],
        CAST(NULL AS DECIMAL(18,4)) AS [PURCHASE_PRICE],
        CAST(ISNULL(TRY_CAST(P.Inf AS NUMERIC),1) AS DECIMAL(18,4)) AS [ORDER_MULTIPLE],
        CAST(ISNULL(TRY_CAST(P.Inf AS NUMERIC),0) AS DECIMAL(18,4)) AS [QTY_PALLET],
        CAST(P.VolU AS DECIMAL(18,6)) AS [VOLUME],
        CAST(P.NWgtU AS DECIMAL(18,6)) AS [WEIGHT],

        CAST(S.[MinBal] AS DECIMAL(18,4)) AS [SAFETY_STOCK_UNITS],
        CAST(NULL AS DECIMAL(18,4)) AS [MIN_DISPLAY_STOCK],

        CAST(S.MaxBal AS DECIMAL(18,4)) AS [MAX_STOCK],
        CAST(p.ProdPrGr AS NVARCHAR(255)) AS [ITEM_GROUP_NO_LVL_1],
        CAST(p.ProdTp2 AS NVARCHAR(255)) AS [ITEM_GROUP_NO_LVL_2],
        IIF(p.ProdTp3 <> 0, CAST(p.ProdNo AS NVARCHAR(255)), NULL) AS [ITEM_GROUP_NO_LVL_3],
        CAST(U.[Descr] AS NVARCHAR(50)) AS [BASE_UNIT_OF_MEASURE],
        CAST(U_Purch.[Descr] AS NVARCHAR(50)) AS [PURCHASE_UNIT_OF_MEASURE],
        CAST(ISNULL(U_Purch.[StUnRt],1) AS DECIMAL(18,4)) AS [QTY_PER_PURCHASE_UNIT],
        CAST(0 AS BIT) AS [SPECIAL_ORDER],
        CAST(0 AS DECIMAL(18,4)) AS [REORDER_POINT],
		CAST(0 AS BIT) AS [INCLUDE_IN_AGR],
		CAST(IIF(P.Gr3 IN (1,4,5), 0, 1) AS BIT) AS CLOSED,
		ROW_NUMBER() OVER (PARTITION BY P.[ProdNo] ORDER BY CASE WHEN D.SupProd IS NOT NULL THEN 1 ELSE 2 END) AS RowNum
    FROM cus.Prod P
		LEFT JOIN [cus].[StcBal] S ON S.[ProdNo]=P.[ProdNo] AND S.[StcNo]=1
		LEFT JOIN [cus].[Stc] ST ON ST.StcNo = S.StcNo 
		LEFT JOIN [cus].[Actor] A ON P.Gr5 = A.SupNo AND S.NrmSup=A.SupNo
		LEFT JOIN [cus].[DelAlt] D ON D.ProdNo=P.ProdNo AND D.SupNo=P.GR5-- AND D.[Srt] in (1,2)
		LEFT JOIN [cus].[Unit] U ON U.[Un] = P.[StSaleUn]
		LEFT JOIN [cus].[Unit] U_Purch ON U_Purch.[Un] = D.[StPurcUn]
		LEFT JOIN [cus].[PrDcMat] PDM ON P.ProdNo = PDM.ProdNo 
		AND RIGHT(PDM.Inf,2) = RIGHT(CAST(YEAR(GETDATE()) AS NVARCHAR),2)
		AND LEFT(PDM.Inf,9) = 'LISTEPRIS'
	  WHERE GR5 > 0
	  )
	  SELECT *
	FROM RankedProd
	WHERE RowNum = 1 

