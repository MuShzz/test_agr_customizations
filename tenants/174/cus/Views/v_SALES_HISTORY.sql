

CREATE VIEW [cus].[v_SALES_HISTORY]
AS
	WITH sales_history 
	AS 
	(
		SELECT 
			it.ITEMID AS ITEM_NO,
			trans.DATEPHYSICAL AS DATE,
			-SUM(CASE WHEN trans.Transtype=0 THEN (trans.Qty/uc.Factor) ELSE 0 END) AS SALE,
			id.INVENTLOCATIONID AS LOCATION_NO,
			trans.CUSTVENDAC AS CUSTOMER_NO
		FROM  cus.INVENTTRANS trans 
		INNER JOIN cus.INVENTDIM id ON trans.INVENTDIMID=id.INVENTDIMID AND id.DATAAREAID=trans.DATAAREAID
		INNER JOIN cus.INVENTTABLE it ON it.ITEMID=trans.ITEMID AND it.DATAAREAID=id.DATAAREAID AND it.ITEMTYPE<>2
		INNER JOIN cus.UNITCONVERT uc ON uc.ITEMID=trans.ITEMID AND uc.DATAAREAID=id.DATAAREAID AND uc.TOUNIT='ks' AND uc.FACTOR <> 0
		INNER JOIN cus.InventTableModule itm ON itm.ITEMID = trans.ITEMID AND uc.FROMUNIT=itm.UNITID AND itm.DATAAREAID=id.DATAAREAID AND itm.MODULETYPE=0
		--INNER JOIN cus.DataAreas ls ON ls.company_id=id.INVENTLOCATIONID
		INNER JOIN core.location_mapping_setup lms ON lms.locationNo=id.INVENTLOCATIONID -- 19.05.2025.DFS Change to filter locations choosen in AGR		WHERE (trans.DATEPHYSICAL >'2007-09-01')
		--AND trans.ITEMID = '136589'
		GROUP BY it.ITEMID, trans.DATEPHYSICAL, id.INVENTLOCATIONID, trans.CUSTVENDAC 
	)

    SELECT 
		ROW_NUMBER() OVER (ORDER BY CAST(ITEM_NO AS NVARCHAR(255)))	AS [TRANSACTION_ID],
        CAST(ITEM_NO AS NVARCHAR(255))								AS [ITEM_NO],
        CAST(LOCATION_NO AS NVARCHAR(255))							AS [LOCATION_NO],
        CAST(DATE AS DATE)											AS [DATE],
        CAST(ISNULL(SUM(SALE), 0) AS DECIMAL(18,4))					AS [SALE],
        CAST(
			CASE 
			WHEN cust.STATISTICSGROUP 
					IN (N'Bónus',N'Hagkaup',N'Olís',N'Stórkaup') -- 2025.06.18.DFS AF-23 Use Costumer groups for certain customers
			THEN cust.STATISTICSGROUP
			ELSE CUSTOMER_NO
		END AS NVARCHAR(255)) AS CUSTOMER_NO,
        CAST('' AS NVARCHAR(255))									AS [REFERENCE_NO],
        CAST(0 AS BIT)												AS [IS_EXCLUDED]
    FROM
        sales_history
		LEFT JOIN cus.CUSTTABLE cust ON cust.ACCOUNTNUM = CUSTOMER_NO -- 2025.06.18.DFS AF-23 Use Costumer groups for certain customers
	GROUP BY CAST(ITEM_NO AS NVARCHAR(255)), CAST(DATE AS DATE), CAST(LOCATION_NO AS NVARCHAR(255)), CAST(CUSTOMER_NO AS NVARCHAR(255)), cust.STATISTICSGROUP, CUSTOMER_NO 

