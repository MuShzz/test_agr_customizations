
CREATE VIEW [ax_cus].[v_SALES_HISTORY]
AS

	SELECT
		CAST(iv.NO AS NVARCHAR(255))														AS [ITEM_NO],
		id.INVENTLOCATIONID																	AS LOCATION_NO,
		CAST(CASE
				WHEN it.DATEPHYSICAL <> DATEFROMPARTS(1900,01,01)
					THEN it.DATEPHYSICAL
				WHEN ISNULL(it.DATEEXPECTED,'1900-01-01') <> DATEFROMPARTS(1900,01,01)
					THEN DATEADD(DAY, -1, CAST(it.DATEEXPECTED AS DATE))
				END AS DATE)																AS [DATE],
		SUM(-CAST(it.QTY AS DECIMAL(18, 4)))												AS SALE,
		CAST('' AS NVARCHAR(255))															AS [CUSTOMER_NO],
		CAST('' AS NVARCHAR(255))															AS [REFERENCE_NO],
		CAST(0 AS BIT)																		AS [IS_EXCLUDED],
		CAST(NULL AS BIGINT)																AS [TRANSACTION_ID]
	FROM
		[ax].INVENTTRANS it
        INNER JOIN [ax].INVENTTRANSORIGIN ito	ON it.INVENTTRANSORIGIN = ito.RECID AND it.PARTITION = ito.PARTITION AND it.DATAAREAID = ito.DATAAREAID
        INNER JOIN [ax_cus].INVENTDIM id		ON id.INVENTDIMID = it.INVENTDIMID AND id.DATAAREAID = it.DATAAREAID AND id.PARTITION = ito.PARTITION
        INNER JOIN ax_cus.Item_v iv				ON iv.No_TO_JOIN_IL = it.ITEMID
	WHERE
		(CAST(it.DATEPHYSICAL AS DATE) <> '1900-01-01'
		OR (CAST(it.DATEPHYSICAL AS DATE) = '1900-01-01' AND ISNULL(it.DATEEXPECTED,'1900-01-01') > DATEADD(DAY, -10, CAST(GETDATE() AS DATE)))  -- Recent dateexpected
		)
		AND ito.REFERENCECATEGORY IN (0, 150)
		--AND it.STATUSISSUE IN (0, 1) -- NOT in PLUS
		AND iv.CLOSED = 0
	GROUP BY
		it.RECID,
        iv.NO,
        id.INVENTLOCATIONID,
        CAST(CASE
				WHEN it.DATEPHYSICAL <> DATEFROMPARTS(1900,01,01)
					THEN it.DATEPHYSICAL
                WHEN ISNULL(it.DATEEXPECTED,'1900-01-01') <> DATEFROMPARTS(1900,01,01)
					THEN DATEADD(DAY, -1, CAST(it.DATEEXPECTED AS DATE))
            END AS DATE)

